param location string = resourceGroup().location
param containerRegistryName string
param containerImage string
param prefix string
param sku string
param envName string

//
// shared ressources
//

resource containerRegistry 'Microsoft.ContainerRegistry/registries@2021-09-01' existing = {
  name: split(containerRegistryName, '.')[0]
  scope: resourceGroup('vsb-devtest-common-rg')
}

resource appServicePlan 'Microsoft.Web/serverfarms@2021-02-01' = {
  name: '${prefix}-asp'
  location: location
  sku: {
    name: 'P0V3'
    tier: sku
  }
  kind: 'linux'
  properties: {
    reserved: true
  }
}

resource logAnalyticsWorkspace 'Microsoft.OperationalInsights/workspaces@2022-10-01' = {
  name: '${prefix}-logs'
  location: location
  properties: {
    sku: {
      name: 'PerGB2018'
    }
    retentionInDays: 30
    features: {
      enableLogAccess: true
      searchVersion: 1
    }
    workspaceCapping: {
      dailyQuotaGb: 1
    }
    publicNetworkAccessForIngestion: 'Enabled'
    publicNetworkAccessForQuery: 'Enabled'
  }
}

resource appInsights 'Microsoft.Insights/components@2020-02-02' = {
  name: '${prefix}-appinsights'
  location: location
  kind: 'web'
  properties: {
    Application_Type: 'web'
    WorkspaceResourceId: logAnalyticsWorkspace.id
  }
}

//
// dedicated ressources
//

resource webApp 'Microsoft.Web/sites@2021-02-01' = {
  name: '${prefix}-${envName}-webapp'
  location: location
  kind: 'app,linux'
  identity: {
    type: 'SystemAssigned'
  }

  properties: {
    serverFarmId: appServicePlan.id
#disable-next-line BCP037 // preview
    AutoGeneratedDomainNameLabelScope: 'TenantReuse'
    siteConfig: {
      linuxFxVersion: 'DOCKER|${containerRegistryName}/${containerImage}'
    }
  }
}

resource webAppSettings 'Microsoft.Web/sites/config@2021-02-01' = {
  parent: webApp
  name: 'appsettings'
  properties: {
    DOCKER_REGISTRY_SERVER_URL: containerRegistry.properties.loginServer
    DOCKER_REGISTRY_SERVER_USERNAME: containerRegistry.listCredentials().username
    DOCKER_REGISTRY_SERVER_PASSWORD: containerRegistry.listCredentials().passwords[0].value

    APPINSIGHTS_INSTRUMENTATIONKEY: appInsights.properties.InstrumentationKey
    APPLICATIONINSIGHTS_CONNECTION_STRING: appInsights.properties.ConnectionString
    ApplicationInsightsAgent_EXTENSION_VERSION: '~3'

    WEBSITE_PORT: '3000'
  }
}

resource diagnosticSettings 'Microsoft.Insights/diagnosticSettings@2021-05-01-preview' = {
  name: '${prefix}-${envName}-diag'
  scope: webApp
  properties: {
    workspaceId: logAnalyticsWorkspace.id
    logs: [
      {
        category: 'AppServiceHTTPLogs'
        enabled: true
      }
      {
        category: 'AppServiceConsoleLogs'
        enabled: true
      }
      {
        category: 'AppServiceAppLogs'
        enabled: true
      }
      {
        category: 'AppServiceAuditLogs'
        enabled: false
      }
      {
        category: 'AppServiceIPSecAuditLogs'
        enabled: false
      }
      {
        category: 'AppServicePlatformLogs'
        enabled: true
      }
    ]
    metrics: [
      {
        category: 'AllMetrics'
        enabled: true
      }
    ]
  }
}

output webAppUrl string = 'https://${webApp.properties.defaultHostName}'
